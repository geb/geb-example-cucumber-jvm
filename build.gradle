buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		// if you use the version of cucumber-jvm that the plugin was built with
		classpath "info.cukes:cucumber-core:1.2.2"
	}
}

plugins {
	id "groovy"
	id "idea"
	id "com.github.samueltbrown.cucumber" version "0.8"
	id "com.energizedwork.idea-project-components" version "1.2"
	id "com.energizedwork.idea-base" version "1.2"
	id "com.energizedwork.webdriver-binaries" version "1.0"
}

ext {
	groovyVersion = '2.4.12'
	gebVersion = '2.1'
	seleniumVersion = '3.6.0'
	cucumberJvmVersion = '1.2.2'
	chromeDriverVersion = '2.32'
	geckoDriverVersion = '0.18.0'
}

repositories {
	mavenCentral()
}

dependencies {
	testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
	testCompile "org.gebish:geb-core:$gebVersion"
	testCompile "org.seleniumhq.selenium:selenium-api:$seleniumVersion"

	testCompile "info.cukes:cucumber-core:$cucumberJvmVersion"
	testCompile "info.cukes:cucumber-groovy:$cucumberJvmVersion"

	// this supplies some pre-built cucumber steps for driving Geb,
	// see https://github.com/tomdcc/geb-cucumber
	testCompile "io.jdev.geb:geb-cucumber:0.3"

	// pick whichever drivers you want. We test against most of them here.
	// see src/test/resources/GebConfig.groovy for how to switch between them
	cucumberRuntime "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
	cucumberRuntime "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
}

webdriverBinaries {
	chromedriver chromeDriverVersion
	geckodriver geckoDriverVersion
}

cucumber {
	formats = [
		'pretty', // prints nice format out to the console
		'html:build/reports/cucumber', // html
		'junit:build/cucumber.xml' // junit format for integration with CI tool etc
	]
	// src/cucumber/resources is included automatically
	glueDirs = [
		"classpath:io.jdev.geb.cucumber.steps.groovy.en"
	]
}

configureChromeDriverBinary {
	addBinaryAware { path ->
		tasks.cucumber.jvmOptions.systemProperties("webdriver.chrome.driver": path)
	}
}

configureGeckoDriverBinary {
	addBinaryAware { path ->
		tasks.cucumber.jvmOptions.systemProperties("webdriver.gecko.driver": path)
	}
}

tasks.cucumber {
	dependsOn configureChromeDriverBinary, configureGeckoDriverBinary
	jvmOptions.systemProperties([
		"geb.cucumber.step.packages": "pages",
		"geb.env": System.getProperty("geb.env")
	])
}

tasks.withType(GroovyCompile) {
	groovyOptions.forkOptions.memoryMaximumSize = '256m'
}

ideaProjectComponents {
	file "gradle/idea/ideaCodeStyle.xml"
}